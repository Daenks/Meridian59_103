% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DesertRiver2 is DesertRoom

constants:

   include blakston.khd

resources:

   include desertriver2.lkod

   room_desertriver2 = desertriver2.roo
   room_name_desertriver2 = "Abyssal Bore"

   waterfall_sound_into_lava = wfall2.wav
   flame_sound = fire02.wav
   quake_sound = earthquake03.wav

   entrance_warning = \
      "~rSulfuric gasses taint the air. Vast heat radiates from the earth. "
      "This is a place of fire and death, and you know instinctively "
      "that ~Ba single misstep here could get you killed."
   rumble_msg = "The bore rumbles ominously."
   lava_kill_msg = \
      "~rAs the lava rises up to meet you, you have time only to regret your "
      "choice of step."
   weak_damage_msg = \
      "~rThe rising heat singes your skin and hurts your lungs."
   painful_damage_msg = \
      "~rThe oppressive heat sears your limbs!"
   deadly_damage_msg = \
      "~rVolcanic heat melts your skin!"
   catastrophic_damage_msg = \
      "~B~rYou are cooking from the inside out!~B"

   cannot_phase_here = \
      "The fabric of reality here rages chaotically around the epicenter of a "
      "rupture. You cannot risk phasing out unless you can manage to reach "
      "the convergence itself."
   phase_success = \
      "You phase out of this plane of existence - and directly into another!"

classvars:

   vrName = room_name_desertriver2

   viTeleport_row = 8
   viTeleport_col = 18

   viTerrain_type = TERRAIN_MOUNTAIN | TERRAIN_LAVA

properties:

   piBaseLight = LIGHT_DARK
   piOutside_factor = OUTDOORS_5 

   piDirectional_percent = DIRECTIONAL_PERCENT_OUTDOORS

   prRoom = room_desertriver2
   piRoom_num = RID_DESERTRIVER2
   pbPrismOpen = FALSE
   
   plLavaDanger = $
   
   ptRumbleTimer = $

messages:

   RecalcLightAndWeather()
   {
      local i;

      switch(Send(SYS,@GetDayPhase))
      {
         case DAY_PHASE_DAWN:
            break;
         case DAY_PHASE_DAY:
            if ptRumbleTimer = $
            {
               ptRumbleTimer = CreateTimer(self,@RumbleBore,6000);
            }
            break;
         case DAY_PHASE_DUSK:
            break;
         case DAY_PHASE_NIGHT:
            break;
      }

      propagate;
   }
   
   FirstUserEntered()
   {
      if ptRumbleTimer = $
      {
         ptRumbleTimer=CreateTimer(self,@RumbleBore,Random(6000,12000));
      }
      propagate;
   }

   LastUserLeft()
   {
      if ptRumbleTimer <> $
      {
         DeleteTimer(ptRumbleTimer);
         ptRumbleTimer = $;
      }
      propagate;
   }

   RumbleBore()
   {
      local i;

      ptRumbleTimer = $;
      Send(self,@Rumble,#duration=3000);

      foreach i in plActive
      {
         if IsClass(First(i),&Player)
         {
            Send(self,@DealLavaHeatDamage,#who=First(i));
         }
      }

      ptRumbleTimer=CreateTimer(self,@RumbleBore,Random(6000,12000));

      return;
   }
   
   DealLavaHeatDamage(who=$)
   {
      local iHeight, iDamage, iDamageResult;
      
      iHeight = Send(who,@GetHeightAtObject);
      
      iDamage = 9000;

      iDamage = Bound(iDamage - 2*iHeight,1,$);
      
      if iDamage > 6600
      {
         Send(who,@MsgSendUser,#message_rsc=catastrophic_damage_msg);
      }
      else if iDamage > 4400
      {
         Send(who,@MsgSendUser,#message_rsc=deadly_damage_msg);
      }
      else if iDamage > 2200
      {
         Send(who,@MsgSendUser,#message_rsc=painful_damage_msg);
      }
      else
      {
         Send(who,@MsgSendUser,#message_rsc=weak_damage_msg);
      }

      Send(who,@WaveSendUser,#wave_rsc=flame_sound);
      
      iDamageResult = Send(who,@AssessDamage,#what=self,
                            #damage=iDamage,
                            #aspell=ATCK_SPELL_FIRE,#report=FALSE,
                            #precision=TRUE);
      
      if iDamageResult = $
      {
         Send(who,@Killed,#what=who);
      }
   
      return;
   }
   
   SomethingMoved(what=$,new_row=0,new_col=0,fine_row=0,fine_col=0)
   {
      local iQflags, iRflags, iHeightF, iHeightFWD, iHeightC, iServerID;

      if new_row < 2 and (new_col < 15)
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTRIVER1),
              #new_row=37,#new_col=6,#fine_row=52,#fine_col=1,
              #new_angle=Send(what,@GetAngle));
         return;
      }

      if new_row < 2 and (new_col > 15)
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTRIVER1),
              #new_row=37,#new_col=18,#fine_row=42,#fine_col=12,
              #new_angle=Send(what,@GetAngle));
              
         return;
      }

      if NOT IsClass(what,&Player)
      {
         propagate;
      }

      iQflags = LIQ_GET_SECTORINFO;

      if GetLocationInfoBSP(
                      prmRoom, iQflags, new_row, new_col, fine_row, fine_col,
                      *iRflags, *iHeightF, *iHeightFWD, *iHeightC, *iServerID)
      {
         % Section 44 is the lava
         if iServerID = 44
         {
            Send(self,@LavaStep,#who=what);
         }
         else
         {
            Send(self,@NonLavaStep,#who=what);
         }
      }

      propagate;
   }
   
   LavaStep(who=$)
   {
      local i;
      
      if who = $
      {
         return;
      }

      foreach i in plLavaDanger
      {
         if First(i) = who
         {
            return;
         }
      }
      
      plLavaDanger = Cons([who,CreateTimer(self,@FellInLava,2000)],
                     plLavaDanger);
      
      return;
   }
   
   NonLavaStep(who=$)
   {
      local i;
      
      if who = $
      {
         return;
      }

      foreach i in plLavaDanger
      {
         if First(i) = who
         {
            DeleteTimer(Nth(i,2));
            SetNth(i,2,$);
            SetNth(i,1,$);
            plLavaDanger = DelListElem(plLavaDanger,i);
            return;
         }
      }
      
      return;
   }
   
   FellInLava(timer=$)
   {
      local i, iQflags, iRflags, iHeightF, iHeightFWD, iHeightC, iServerID;
      
      foreach i in plLavaDanger
      {
         if Nth(i,2) = timer
         {
            SetNth(i,2,$);
            iQflags = LIQ_GET_SECTORINFO;
            
            if Send(First(i),@GetOwner) <> self
            {
               plLavaDanger = DelListElem(plLavaDanger,i);
               return;
            }

            if GetLocationInfoBSP(
                      prmRoom, iQflags, Send(First(i),@GetRow),
                      Send(First(i),@GetCol),
                      Send(First(i),@GetFineRow),
                      Send(First(i),@GetFineCol),
                      *iRflags, *iHeightF, *iHeightFWD, *iHeightC, *iServerID)
            {
               % They're still in the lava!
               if iServerID = 44
               {
                  Send(self,@LavaBootPlayer,#who=First(i));
               }
            }
            
            plLavaDanger = DelListElem(plLavaDanger,i);
         }
      }
      
      return;
   }
   
   LavaBootPlayer(who=$)
   {
      Send(who,@MsgSendUser,#message_rsc=lava_kill_msg);
      Send(who,@Killed);
      return;
   }

   CreateStandardExits()
   {
      plEdge_Exits = $;

      propagate;
   }
   
   Constructed()
   {
      plLooping_sounds = [[ waterfall_sound_into_lava, 12, 16, 120, 100 ]];
      plLooping_sounds = Cons([ quake_sound, 0, 0, 300, 100],plLooping_sounds);
      plLooping_sounds = Cons([ room_lava_sound, 0, 0, 300, 100 ],
                              plLooping_sounds);
      propagate;
   }

   CreateStandardObjects()
   {
      Send(self,@CreateFogClouds);

      propagate;
   }
   
   Delete()
   {
      local i;

      Send(self,@DeleteFogClouds);
      
      foreach i in plLavaDanger
      {
         DeleteTimer(Nth(i,2));
         SetNth(i,2,$);
         SetNth(i,1,$);
         plLavaDanger = DelListElem(plLavaDanger,i);
      }
      
      if ptRumbleTimer <> $
      {
         DeleteTimer(ptRumbleTimer);
         ptRumbleTimer = $;
      }
      
      propagate;
   }

   StartRain()
   {
      local i;
      
      return;

      if Send(SYS,@GetDayPhase) <> DAY_PHASE_DUSK
      {
         return;
      }

      Send(self,@ChangeTexture,#id=14,#new_texture=09363,
            #flags=CTF_BELOWWALL);

      propagate;
   }
   
   EndRain()
   {
      return;
   }
   
   NewHold(what = $)
   {
      if IsClass(what,&Player)
      {
         Send(what,@MsgSendUser,#message_rsc=entrance_warning);
      }
      propagate;
   }
   
   CreateFogClouds()
   {
      Send(self,@NewHold,#what=Create(&FogCloud,#Duration=$),#new_row=12,
            #new_col=16,#fine_row=43,#fine_col=20);
      Send(self,@NewHold,#what=Create(&FogCloud,#Duration=$),#new_row=12,
            #new_col=16,#fine_row=0,#fine_col=0);
      Send(self,@NewHold,#what=Create(&FogCloud,#Duration=$),#new_row=12,
            #new_col=16,#fine_row=63,#fine_col=63);
      Send(self,@NewHold,#what=Create(&FogCloud,#Duration=$),#new_row=12,
            #new_col=16,#fine_row=56,#fine_col=12);
      Send(self,@NewHold,#what=Create(&FogCloud,#Duration=$),#new_row=14,
            #new_col=19,#fine_row=44,#fine_col=36);
      Send(self,@NewHold,#what=Create(&FogCloud,#Duration=$),#new_row=14,
            #new_col=19,#fine_row=63,#fine_col=63);
      Send(self,@NewHold,#what=Create(&FogCloud,#Duration=$),#new_row=14,
            #new_col=19,#fine_row=0,#fine_col=0);
      Send(self,@NewHold,#what=Create(&FogCloud,#Duration=$),#new_row=14,
            #new_col=19,#fine_row=56,#fine_col=12);
      Send(self,@NewHold,#what=Create(&FogCloud,#Duration=$),#new_row=14,
            #new_col=17,#fine_row=43,#fine_col=18);

      return;
   }
   
   DeleteFogClouds()
   {
      local i;
      
      foreach i in plPassive
      {
         if IsClass(First(i),&FogCloud)
         {
            Send(First(i),@Delete);
         }
      }
      
      return;
   }
   
   OpenPrismOfFireEntrance()
   {
      Send(self,@SetSector,#sector=46,
            #animation=ANIMATE_FLOOR_LIFT,#height=0,#speed=40);
      % Texture the portal..
      Send(self,@ChangeTexture,#id=46,#new_texture=07702,
            #flags=CTF_FLOOR);
      pbPrismOpen = TRUE;
      return;
   }
   
   ClosePrismOfFireEntrance()
   {
      Send(self,@RemoveTextureChange,#id=46);
      Send(self,@SetSector,#sector=46,
            #animation=ANIMATE_FLOOR_LIFT,#height=6,#speed=1);
      pbPrismOpen = FALSE;
      return;
   }
   
   StartBrightHeat()
   {
      % No need for region's heat danger. Trust me.
      return;
   }
   
   EndBrightHeat()
   {
      return;
   }

   StartSnow(bOnGround=TRUE)
   {
      return;
      
      if bOnGround
      {
         if pbSnowGroundTexture
         {
            % Hell freezes over at night.
         
            % Add snow to floor regions that are also sometimes lava.
            Send(self,@ChangeTexture,#id=44,#new_texture=61015,
                  #flags=CTF_FLOOR);
            Send(self,@SetSectorFlags,#sector=44,#depth=SF_DEPTH0,
                  #scrollSpeed=SCROLL_NONE);

            % Frozen waterfall makes no steam.
            Send(self,@DeleteFogClouds);
            % Remove waterfall sound.
            Send(self,@RemoveLoopingSound,#sound_rsc=waterfall_sound_into_lava);
         }
      }

      propagate;
   }

   EndSnow(override=FALSE)
   {
      local i;
      
      return;

      if pbSnowGroundTexture
         OR override
      {
         % Remove snow from lava floors.
         Send(self,@RemoveTextureChange,#id=44);
         Send(self,@RemoveSectorFlagChange,#id=44);

         % Resume steam.
         Send(self,@CreateFogClouds);
         plLooping_sounds = [[ waterfall_sound_into_lava, 12, 16, 65, 100 ]];
      }

      propagate;
   }

   BeginHeat()
   {
      % Prism of Fire opens during the Day phase
      Send(self,@OpenPrismOfFireEntrance);
      propagate;
   }
   
   EndHeat()
   {
      Send(self,@ClosePrismOfFireEntrance);
      propagate;
   }

   ReqSpellCast(who = $, oSpell = $)
   {
      local iQflags, iRflags, iHeightF, iHeightFWD, iHeightC, iServerID;

      if IsClass(oSpell,&Phase)
      {
         if NOT IsClass(who,&Player)
         {
            propagate;
         }

         iQflags = LIQ_GET_SECTORINFO;

         if GetLocationInfoBSP(
                      prmRoom, iQflags, Send(who,@GetRow),
                      Send(who,@GetCol),
                      Send(who,@GetFineRow),
                      Send(who,@GetFineCol),
                      *iRflags, *iHeightF, *iHeightFWD, *iHeightC, *iServerID)
         {
            if iServerID = 46
               AND pbPrismOpen
            {
               Post(who,@MsgSendUser,#message_rsc=phase_success);
               Post(Send(SYS,@FindRoomByNum,#num=RID_ELEMENTAL_DUNGEON1),
                     @Teleport,#what=who);
               return FALSE;
            }
            else
            {
               Send(who,@MsgSendUser,#message_rsc=cannot_phase_here);
               return FALSE;
            }
         }
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

