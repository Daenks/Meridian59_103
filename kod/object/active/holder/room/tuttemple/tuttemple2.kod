% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
TutorialTemple2 is TutorialTemple

constants:

   include blakston.khd

resources:

   room_name_tuttemple2 = "Temple of Knowledge"
   room_tuttemple2 = tuttemple2.roo
   tuttemple2_music = login.mp3

   tuttemple2_door_sound = down2.wav

classvars:

   viTeleport_row = 8
   viTeleport_col = 4
   viTeleport_angle = ANGLE_NORTH

properties:

   vrName = room_name_tuttemple2
   prRoom = room_tuttemple2
   piRoom_num = RID_TUTORIAL_TEMPLE2
   viTerrain_type = TERRAIN_CITY

   prMusic = tuttemple2_music

messages:

   CreateStandardExits()
   {
      plExits = $;

      %% TO TOWN
  %    plExits = Cons([ 4, 10, RID_MARION, 32, 17, ROTATE_NONE ],plExits);
   %   plExits = Cons([ 5, 10, RID_MARION, 32, 17, ROTATE_NONE ],plExits);

      propagate;
   }

   Teleport(what=$)
   "Admin supported."
   "Teleports object to coordinates specified in class vars."
   {
      if Send(self,@GetTeleportRow) <> $ AND Send(self,@GetTeleportCol) <> $
      {
         Send(SYS,@UtilGoNearSquare,#what=what,#where=self,
              #new_row=Send(self,@GetTeleportRow),
              #new_col=Send(self,@GetTeleportCol),
              #fine_row=0,
              #fine_col=0,
              #new_angle=Send(self,@GetTeleportAngle));

         return TRUE;
      }

      return FALSE;
   }

   CreateStandardObjects()
   {
      Send(self,@NewHold,#what=Create(&TutorialWizard2b),
            #new_row=8,#new_col=18,#fine_row=39,#fine_col=19);

      propagate;
   }

   SomeoneSaid(what = $,type = $,string = $,parm1 = $,parm2 = $,parm3 = $,
               parm4 = $, parm5 = $,parm6 = $,parm7 = $,parm8 = $)
   {
      local i, each_obj;

      if NOT isClass(what,&player) OR type = SAY_YELL
      {
         propagate;
      }

      if StringEqual(string,"yes")
      {
         Send(self,@SomethingWaveRoom,#wave_rsc=tuttemple_door_sound);
         Send(self,@SetSector,#sector=1,#animation=ANIMATE_CEILING_LIFT,
              #height=130,#speed=16);
         Send(self,@SetSector,#sector=2,#animation=ANIMATE_CEILING_LIFT,
              #height=85,#speed=5);
      }

      if StringEqual(string,"no")
      {
         Send(self,@SomethingWaveRoom,#wave_rsc=tuttemple_door_sound);
         Send(self,@SetSector,#sector=1,#animation=ANIMATE_CEILING_LIFT,
              #height=130,#speed=16);
         Send(self,@SetSector,#sector=3,#animation=ANIMATE_CEILING_LIFT,
              #height=70,#speed=5);
         foreach i in plPassive
         {
            each_obj = Send(self,@HolderExtractObject,#data=i);
            if IsClass(each_obj,&DynamicLight)
            {
               Send(each_obj,@Delete);
            }
         }
      }

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
