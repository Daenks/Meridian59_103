% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BarloqueJail is BarloqueRoom

constants:

   include blakston.khd

resources:

   room_barloque_jail = barjail.roo
   room_name_barloque_jail = "Old Jailhouse"

classvars:

   vrName = room_name_barloque_jail

   viTeleport_row = 6
   viTeleport_col = 12

   viPermanent_flags = ROOM_SAFELOGOFF
   viTerrain_Type = TERRAIN_CITY

properties:

   prRoom = room_barloque_jail
   piRoom_num = RID_BAR_JAIL

   piBaseLight = LIGHT_NICE
   piOutside_factor = 3

   pbLocked = TRUE

messages:

   CreateStandardObjects()
   {
      %% guards
      send(self,@NewHold,#what=Create(&DukeGuard),
	 #new_row=3,#new_col=14,#fine_row=32,#fine_col=0,
	 #new_angle=ANGLE_SOUTH);
      send(self,@NewHold,#what=Create(&DukeGuard),
	 #new_row=14,#new_col=14,#fine_row=32,#fine_col=0,
	 #new_angle=ANGLE_NORTH);
      send(self,@NewHold,#what=Create(&DukeGuard),
	 #new_row=2,#new_col=2,#fine_row=32,#fine_col=32,
	 #new_angle=ANGLE_SOUTH_EAST);

      %% table
      send(self,@NewHold,#what=Create(&Table),#new_row=4,#new_col=11);

      propagate;
   }

   CreateStandardExits()
   {
      plExits = $;

      %% TO TOWN
      plExits = Cons([ 2, 7, RID_BAR_NORTH, 31, 62, ROTATE_NONE ],plExits); %% north
      plExits = Cons([ 6, 2, ROOM_LOCKED_DOOR ],plExits); %% to courtroom

      % these doors moved below to allow locking.
      %% THROUGH SECURITY
      %plExits = Cons([ 4, 4, RID_BAR_JAIL, 3, 2, ROTATE_NONE ],plExits); %% to courtroom
      %plExits = Cons([ 3, 3, RID_BAR_JAIL, 4, 5, ROTATE_NONE ],plExits); %% to courtroom

      propagate;
   }

   SomethingTryGo(what = $,row = $,col = $)
   {
      if (row = 3) and (col = 3)
      {
         if pbLocked
         {
            send(what,@msgsenduser,#message_rsc=room_door_is_locked);
            Send(what,@WaveSendUser,#wave_rsc=room_sound_locked);
            return TRUE;
         }
         else
         {
            Send(what,@MsgSendUser,#message_rsc=room_door_was_opened);
            Send(what,@WaveSendUser,#wave_rsc=Send(self,@GetDoorSound));
            Send(SYS,@UtilGoNearSquare,#what=what,#where=self,#new_row=4,#new_col=5);
            return TRUE;
         }
      }
      if (row = 4) and (col = 4)
      {
         if pbLocked
         {
            send(what,@msgsenduser,#message_rsc=room_door_is_locked);
            Send(what,@WaveSendUser,#wave_rsc=room_sound_locked);
            return TRUE;
         }
         else
         {
            Send(what,@MsgSendUser,#message_rsc=room_door_was_opened);
            Send(what,@WaveSendUser,#wave_rsc=Send(self,@GetDoorSound));
            Send(SYS,@UtilGoNearSquare,#what=what,#where=self,#new_row=3,#new_col=2);
            return TRUE;
         }
      }
      propagate;
   }

   ReqSomethingAttack(what = $,victim = $,use_weapon = $)
   "No one may attack here."
   {
      Send(what,@MsgSendUser,#message_rsc=room_no_attack);

      return FALSE;
   }

   ReqSpellCast(who = $, oSpell = $, lItems = $)
   "Only DM-level characters may cast spells here."
   {
      % no-magic zone
      if IsClass(oSpell,&Spell) AND (NOT IsClass(who,&DM))
      {
         Send(who,@MsgSendUser,#message_rsc=room_no_spell);
         send(self,@SomeoneSaid,#what=self,#type=SAY_MESSAGE,#string=oog_casting_message,
              #parm1=send(who,@GetTrueName),#parm2=send(oSpell,@GetTrueName));

         return FALSE;
      }

      propagate;
   }

   RoomReqCommunication(who=$,type=$,string=$,users=$)
   {
       local i;

       if IsClass(who,&DM)
       {  return TRUE;  }

       if type = SAY_GUILD or type = SAY_EVERYONE or type = SAY_YELL
       {
          Send(who,@MsgSendUser,#message_rsc=room_no_sound);

          return FALSE;
       }

       if type = SAY_GROUP or type = SAY_GROUP_ONE
       {
          for i in users
          {
             if not isClass(i,&DM)
             {
                Send(who,@MsgSendUser,#message_rsc=room_no_sound);

                return FALSE;
             }
          }
       }

       return TRUE;
   }

   SeanceCheck()
   {
      return FALSE;
   }

   CanHavePlayerPortal()
   {
      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
