% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GiantRat is Monster

constants:

   include blakston.khd

resources:

   giantrat_koc_name_rsc = "napyijoa"
   giantrat_name_rsc = "giant rat"
   giantrat_icon_rsc = rat.bgf
   giantrat_desc_rsc = \
   "The giant rat bares its yellow teeth in defiance.  A "
   "smell of rot emanates from its filthy coat."

   giantrat_dead_icon_rsc = ratX.bgf
   giantrat_dead_name_rsc = "dead giant rat"

   giantrat_sound_hit = rat_atkh.wav
   giantrat_sound_miss = rat_atkm.wav
   giantrat_sound_death = rat_dth.wav
   giantrat_sound_aware = rat_awr.wav
   
   giantrat_charmed = \
      "The giant rat scarfs down the %s and looks at you with "
      "obvious adoration."
   giantrat_notcharmed = \
      "The giant rat scarfs down the %s, sniffs your hand, and looks at "
      "you expectantly."
   GiantRat_unwanted_give = \
      "~k%s%s cocks its head quizzically and sniffs your hand.~n~k"
   

classvars:

   vrKocName = giantrat_koc_name_rsc
   vrName = giantrat_name_rsc
   vrIcon = giantrat_icon_rsc
   vrDesc = giantrat_desc_rsc
   vrDead_icon = giantrat_dead_icon_rsc
   vrDead_name = giantrat_dead_name_rsc

   viTreasure_type = TID_RAT

   viSpeed = SPEED_FAST
   viAttack_type = ATCK_WEAP_CLAW
   viAttributes = MOB_RECEIVE
   viLevel = 30
   viDifficulty = 1
   viVisionDistance = 6
   viKarma = -20
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_FIGHT_SWITCHALOT
   viCashmin = 150
   viCashmax = 250

   vrSound_hit = giantrat_sound_hit
   vrSound_miss = giantrat_sound_miss
   vrSound_aware = giantrat_sound_aware
   vrSound_death = giantrat_sound_death
   
   vrUnwanted_give = GiantRat_unwanted_give

properties:

   piAnimation = ANIM_NONE

messages:

   Constructor()
   {
      plWantedItems = [&Food];

      propagate;
   }

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_FIRE, -20],
                        [ATCK_WEAP_BLUDGEON, -10],
                        [ATCK_WEAP_SLASH, 20] ];
      propagate;
   }

   MonsterAttack(what = $)
   {
      % add in any attack animation calls
      Send(self,@DoSlash);

      return;
   }

   DoSlash()
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,125,2,2,2,6);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,75,2,2,2,6);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,100,2,7,2,11,2,1);

         return;
      }

      % if no body animation

      propagate;
   }

   IsAlly(target = $)
   {
      if IsClass(target,&GiantRat)
      {
         return TRUE;
      }
   
      return FALSE;
   }
   
   CanAcceptOffer()
   {
      return TRUE;
   }

   GotWantedItem(who=$, obj = 0)
   {
      local oPlayerObj, i;

      if NOT IsClass(obj,&Food)
      {
         return FALSE;
      }

      oPlayerObj = $;
      for i in Send(who,@GetHolderPassive)
      {
         if IsClass(i,GetClass(obj))
         {
            oPlayerObj = i;

            break;
         }
      }

      if oPlayerObj = $
      {
         Debug("player offered some food he didn't have.");

         return FALSE;
      }

      if Send(oPlayerObj,@GetNumber) < Send(obj,@GetNumber)
      {
         Debug("Player offered more food than he had.");

         return FALSE;
      }

      if IsClass(obj,&Cheese)
         AND (poMaster = $ OR IsClass(poMaster,&User))
      {
         %% GiantRat is charmed.
         Send(self,@SetMaster,#oMaster=who);
         Post(self,@ResetBehaviorFlags);
         Post(who,@MsgSendUser,#message_rsc=GiantRat_charmed,
               #parm1=Send(obj,@GetName));
      }
      else
      {
         Post(who,@MsgSendUser,#message_rsc=GiantRat_notcharmed,
               #parm1=Send(obj,@GetName));
      }

      Send(oPlayerObj,@SubtractNumber,#number=Send(obj,@Getnumber));
      Send(obj,@Delete);

      return TRUE;
   }

   TweakBehavior(mob = $)
   {
      if poMaster <> $
      {
         Send(self,@SetBehaviorFlag,#flag=AI_MOVE_FOLLOW_MASTER,#value=TRUE);
         Send(self,@SetBehaviorFlag,#flag=AI_FIGHT_AGGRESSIVE,#value=FALSE);
         Send(self,@SetBehaviorFlag,#flag=AI_FIGHT_NEWBIESAFE);
         Send(self,@SetBehaviorFlag,#flag=AI_FIGHT_MONSTERS);
      }

      return;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
