% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
IllusionaryWallOfFire is ActiveWallElement

constants:

   include blakston.khd

resources:

classvars:

   vrName = WallofFire_name_rsc
   vrIcon = WallofFire_icon_rsc
   vrdesc = WallofFire_desc_rsc

   viObject_flags = LOOK_NO | DRAWFX_TRANSLUCENT_50 | MOVEON_NOTIFY | FLICKERING_YES

   vrDissipateMessage = firewall_dissipates
   vrUnaffectedMessage = firewall_damage0

   viLightBonus = 15
   viIllusion = TRUE

properties:

   piSpellpower = 0

messages:

   Constructor(seed=$)
   {
      piSpellpower = Nth(seed,2);

      propagate;
   }

   DoEffect(what = $)
   {
      local iDamage, oIllusWounds, iDuration, rMessage;

      iDamage = 0;

      if (piSpellpower < 35)
      {
         return;
      }

      oIllusWounds = Send(SYS,@FindSpellByNum,#num=SID_ILLUSIONARY_WOUNDS);
      iDuration = Send(oIllusWounds,@GetDuration,#iSpellPower=piSpellpower);
      iDamage = Send(oIllusWounds,@GetHPLoss,#who=poCaster,#victim=what,#iSpellPower=piSpellpower);

      if iDamage > 0
      {
         % We technically don't have a damage type, but we count as spell damage.
         %  Also, don't lower damage based on resistances.
         iDamage = Send(what,@AssessDamage,#what=poCaster,#damage=Random(0,iDamage),
                        #aspell=ATCK_SPELL_ALL,#absolute=TRUE,#report=FALSE,
                        #report_resistance=FALSE);
         Send(what,@StartEnchantment,#what=oIllusWounds,#time=iDuration,#lastcall=TRUE,
              #state=iDamage,#addicon=FALSE);
      }

      if iDamage = $
      {
         Send(poCaster,@KilledSomething,#what=what,#use_weapon=self);
      }

      if IsClass(poCaster,&Player)
      {
         if iDamage = $
         {
            Send(poCaster,@SetPlayerFlag,#flag=PFLAG_DID_DAMAGE,#value=TRUE);
            
            return;
         }
         else
         {
            if piSpellpower > 0
            {
               Send(poCaster,@SetPlayerFlag,#flag=PFLAG_DID_DAMAGE,#value=TRUE);
            }
         }
      }

      if IsClass(what,&Player)
      {
         if iDamage = 0
         {
            rMessage = firewall_damage0;
         }
         else
         {
            % Damage is greater than 0
            Send(what,@WaveSendUser,#wave_rsc=firewall_burn_snd);

            if iDamage < 5
            {
               rMessage = firewall_damage5;
            }
            else
            {
               if iDamage < 10
               {
                  rMessage = firewall_damage10;
               }
               else
               {
                  if iDamage < 15
                  {
                     rMessage = firewall_damage15;
                  }
                  else
                  {
                     rMessage = firewall_damage20;
                  }
               }
            }
         }

         Send(what,@MsgSendUser,#message_rsc=rMessage);
      }
      
      propagate;
   }

   SendAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE, 4,random(100,140), 2,1, 2,5);
      
      return;
   }

   NewOwner(what = $)
   {
      % new room
      if what <> $
      {
         Send(what,@AddBaseLight,#amount=viLightBonus);
      }

      % old room
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=-viLightBonus);
      }

      propagate;
   }

   Delete()
   {
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=-viLightBonus);
      }

      propagate;
   }

   SendLightingInformation()
   {
      AddPacket(2,(LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC));
      % 5 out of 255 intensity of light
      AddPacket(1,5);


      % Red color
      AddPacket(2,LIGHT_RED);

      return;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
