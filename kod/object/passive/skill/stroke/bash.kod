% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Bash is Stroke

constants:

   include blakston.khd

resources:
   bash_name_rsc = "bash"
   bash_icon_rsc = armorico.bgf
   bash_desc_rsc = "Bashing allows you to do better damage with your shield."

   bash_skill_intro = "Weaponcraft Lv. 3: An advanced technique for dealing more damage when blocking an attack."

classvars:

   vrName = bash_name_rsc
   vrIcon = bash_icon_rsc
   vrDesc = bash_desc_rsc

   vrSkill_intro = bash_skill_intro

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSchool = SKS_FENCING
   viSkill_Num = SKID_BASH
   viSkill_level = 3

	% Chance to increase is one in viChance_To_Increase.
   viChance_To_Increase = 5

   viskillExertion = 0
   vbCheck_exertion = FALSE

   viDamage_factor = 100

properties:

   plPrerequisites = $

messages:

   GetRequisiteStat(who=$)
   "Varies from skill to skill and spell to spell."
   {
      return Send(who,@GetMight);
   }
	
   FindDamage(weapon_used=$, who=$, victim=$)
   "We put this here so that we can vary a stroke's damage per weapon."
   {
      local iAbility, damage;
      
		weapon_used = send(who,@LookupPlayerShield);
		
      % base weapon damage
      damage = send(weapon_used,@GetDamage,#who=who,#target=victim);
      
      % Scale the basedamage with the attacker's ability in the stroke
      iAbility = Send(who,@GetSkillAbility,#skill_num=viSkill_num);
      damage = damage * (50 + (iAbility + 1)/2) / 100;
      
      damage = send(self,@DamageFactors,#damage=damage,#who=who,#weapon_used=weapon_used);
	  
	  return damage;
   }
   
   DamageFactors(damage=0, who=$, weapon_used = $)
   "Different skills/strokes are affected by different stats differently."
   {
      local stat;
	  
	  % Get proficiency bonus.
	  damage = damage + (Send(who,@GetSkillAbility,#Skill_num=SKID_BASH)+1)*damage/100;
	  
      % Get stat damage bonus.
      stat = send(who,@GetMight);
      damage = (50+stat)*damage/100;

      return damage;
   }
	
   ImproveStroke(who=$,use_weapon=$,target=$)
   {
		send(self,@ImproveAbility,#who=who,#target=target);
		
		return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
