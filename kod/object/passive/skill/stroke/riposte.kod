% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Riposte is Stroke

constants:

   include blakston.khd

resources:
   riposte_name_rsc = "riposte"
   riposte_icon_rsc = parryico.bgf
   riposte_desc_rsc = "Riposting allows you to retaliate after successfully parrying an attack."

   riposte_skill_intro = "Weaponcraft Lv. 5: Retaliate after parrying."

classvars:

   vrName = riposte_name_rsc
   vrIcon = riposte_icon_rsc
   vrDesc = riposte_desc_rsc

   vrSkill_intro = riposte_skill_intro

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSchool = SKS_FENCING
   viSkill_Num = SKID_RIPOSTE
   viSkill_level = 5
   viChance_to_increase = 20

   viskillExertion = 0
   vbCheck_exertion = FALSE

   viDamage_factor = 100

properties:

   plPrerequisites = $

messages:

   GetRequisiteStat(who=$)
   "Varies from skill to skill and spell to spell."
   {
      return Send(who,@GetAim);
   }
	
   FindDamage(weapon_used=$, who=$, victim=$)
   "We put this here so that we can vary a stroke's damage per weapon."
   {
      local iAbility, damage;
      
		weapon_used = send(who,@LookupPlayerWeapon);
		
      % base weapon damage
      damage = send(weapon_used,@GetDamage,#who=who,#target=victim);
      
      % Scale the basedamage with the attacker's ability in the stroke
      iAbility = Send(who,@GetSkillAbility,#skill_num=viSkill_num);
      damage = damage * (50 + (iAbility + 1)/2) / 100;
      
      damage = send(self,@DamageFactors,#damage=damage,#who=who,#weapon_used=weapon_used);
	  
	  return damage;
   }
   
   DamageFactors(damage=0, who=$, weapon_used = $)
   "Different skills/strokes are affected by different stats differently."
   {
      local stat;
	  
	  % Get proficiency bonus.
	  damage = damage + (Send(who,@GetSkillAbility,#Skill_num=SKID_BASH)+1)*damage/100;
	  
      % Get stat damage bonus.
      stat = send(who,@GetAim);
      damage = (50+stat)*damage/100;

      return damage;
   }
	
   ImproveStroke(who=$,use_weapon=$,target=$)
   {
		send(self,@ImproveAbility,#who=who,#target=target);
		send(use_weapon,@ImproveProficiency,#who=who,#target=target);
		
		return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
