% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Frailty is Hex

constants:

   include blakston.khd

resources:

   frailty_cast_rsc = \
      "%s%s suddenly seems frail."
   frailty_already_enchanted = \
      "That poor soul is already afflicted by frailty."
   frailty_start = "You suddenly feel frail."
   frailty_stop  = "The feeling of frailty passes."

   frailty_name_rsc = "frailty"
   frailty_icon_rsc = ienfeebl.bgf
   frailty_desc_rsc = \
      "Weakens the target's bones, making them more vulnerable to "
      "physical assault. "
      "This hex requires two orc teeth to cast."

   frailty_sound = qenfeebl.wav

classvars:

   vrName = frailty_name_rsc
   vrIcon = frailty_icon_rsc
   vrDesc = frailty_desc_rsc

   vrAlreadyEnchanted = frailty_already_enchanted
   vrEnchantment_On = frailty_start
   vrEnchantment_Off = frailty_stop
   vrSuccess = frailty_cast_rsc

   viSpell_num = SID_HEX_FRAILTY
   viSchool = SS_WITCHERY
   viSpell_level = 3
   viMana = 20
   viSpell_Exertion = 15

   viFlash = FLASH_BAD

   vrSucceed_wav = frailty_sound
   viChance_To_Increase = 15
   viMeditate_ratio = 40

properties:

   % Increases physical damage taken by this percent
   piBaseEffect = 10

   piFlatDuration = 12000

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&OrcTooth,3],plReagents);

      return;
   }
   
   ModifyDefenseDamage(who=$,what=$,damage=$,atype=0,aspell=0,iHexEffect=0)
   {
      if atype <> 0
      {
         % partially magical attack, half effect
         if aspell <> 0
         {
            damage = damage*(100 + (((piBaseEffect * iHexEffect)/100)/2))/100;
         }
         else
         {
            damage = damage*(100 + ((piBaseEffect * iHexEffect)/100)) / 100;
         }
      }

      return damage;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
