% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%S%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KillingFields is RoomEnchantment

constants:

   include blakston.khd

   %  2 realtime minutes
   KILLINGFIELDS_TIME = 120000

resources:

   killingfields_name_rsc = "killing fields"
   killingfields_icon_rsc = ikillfel.bgf
   killingfields_desc_rsc = \
      "Induces magical blood-lust, increasing the damage of all attacks in the area.  "
	   "Requires elderberries and orc teeth to cast."
   
   killingfields_already_enchanted = "This place is already a killing field."

   killingfields_on = "Kraanan smiles and battle fury fills your heart.  The very earth thirsts for freshly shed blood."
   killingfields_off = "The spirit of Kraanan departs from this place."
   killingfields_on_new_entrant = "Battle fury fills your heart.  The very earth thirsts for freshly shed blood."

classvars:

   vrName = killingfields_name_rsc
   vrIcon = killingfields_icon_rsc
   vrDesc = killingfields_desc_rsc

   viSpell_num = SID_KILLING_FIELDS
   viMana = 9
   viSchool = SS_KRAANAN
   viSpell_level = 5

   viChance_To_Increase = 35
   viMeditate_ratio = 30

properties:
  
messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Elderberry,3],plReagents);
      plReagents = Cons([&OrcTooth,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      local oRoom;

      oRoom = Send(who,@GetOwner);

      % check for enchantment already applied
      if Send(oRoom,@IsEnchanted,#what=self)
      {
         Send(who,@MsgSendUser,#message_rsc=killingfields_already_enchanted);
         
         return FALSE;
      }

      propagate;
   }

   CastSpell(who = $,iSpellPower = 0)
   {
      local oRoom;

      oRoom = Send(who,@GetOwner);

      % Global effects of the enchantment.
      Send(oRoom,@SomeoneSaid,#type=SAY_MESSAGE,#string=killingfields_on,#what=self);

      Send(oRoom,@RoomStartEnchantment,#what=self,#state=iSpellPower,
           #time=send(self,@GetDuration,#iSpellPower=iSpellPower));
      Send(oRoom,@EnchantAllOccupants,#what=self);

      propagate;
   }

   StartEnchantmentNewOccupant(who = $)
   "Called on new occupants of the enchanted room."
   {
      Send(who,@MsgSendUser,#message_rsc=killingfields_on_new_entrant);
      Send(self,@StartEnchantment,#who=who);

      return;
   }

   StartEnchantment(who = $)
   "Starts enchantment effect on one player"
   {
      local iSpellpower;

      iSpellpower = send(send(who,@GetOwner),@GetEnchantmentState,#what=self);
      Send(who,@AddCombatModMelee,#amount=iSpellpower*2);
      Send(who,@AddCombatModRanged,#amount=iSpellpower*2);
      Send(who,@AddCombatModMagic,#amount=iSpellpower*2);

      return;
   }

   EndSpell(where = $,state = $)
   "Called when spell expires."
   {
      Send(where,@SomeoneSaid,#type=SAY_MESSAGE,#string=killingfields_off,#what=self);
      Send(where,@UnenchantAllOccupants,#what=self,#state=state);

      return;
   }

   EndRoomEnchantment(who = $,state = $)
   "Remove enchantment effects on this occupant"
   {
      Send(who,@AddCombatModMelee,#amount=-state*2);
      Send(who,@AddCombatModRanged,#amount=-state*2);
      Send(who,@AddCombatModMagic,#amount=-state*2);

      return;
   }

   GetDuration(iSpellPower=0)
   {
      local duration;

      % Time in seconds.
      duration = 50 + iSpellPower;
      % Convert to ms.
      duration = duration * 1000;

      return duration;
   }

   EffectDesc(who=$)
   {
      local i, iState;

      AppendTempString("\n\n");
      AppendTempString("The ");
      AppendTempString(Send(self,@GetName));
      AppendTempString(" enchantment currently adds 2-5 damage to all attacks in the area.");
      
      return;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
