% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
FireWall is WallSpell

constants:
   include blakston.khd

resources:

   FireWall_name_rsc = "firewall"
   FireWall_icon_rsc = iwalfire.bgf
   FireWall_desc_rsc = \
      "Creates a wall of fire in front of the caster.  "
      "Requires red mushrooms and firesand to cast."

   FireWall_cast_rsc = "A blazing hot wall of fire appears before you!"
   FireWall_failed_rsc = \
      "There is too much summoning magic focused here to create a wall of "
      "fire." 
   FireWall_sound = farenwala822m.wav

classvars:

   vrName = FireWall_name_rsc
   vrIcon = FireWall_icon_rsc
   vrDesc = FireWall_desc_rsc

   viSpell_num = SID_FireWall
   viSchool = SS_FAREN
   viSpell_level = 4
   viMana = 15
   viSpellExertion = 10
   viMeditate_ratio = 30

   viCast_time = 1000
   vrSucceed_wav = FireWall_sound

   viHarmful = TRUE

   vrSummonLimitMsg = FireWall_failed_rsc

properties:


messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&RedMushroom,3],plReagents);
      plReagents = Cons([&Firesand,2], plReagents);

      return;
   }

   CastSpell(who = $, lTargets = $, iSpellPower = 1)
   {
      Send(who,@MsgSendUser,#message_rsc=FireWall_cast_rsc);

      propagate;
   }

   PlaceWallElements(seed = $)
   {
      local lSeeda, lSeedb, lSeedc, lSeedd, lSeede, lSeedf, iDirection, iXStep, iYStep;

      iDirection = Nth(seed,6);
      iXStep = Send(self,@GetXStep,#direction=iDirection);
      iYStep = Send(self,@GetYStep,#direction=iDirection);

      % The standard seed is modified here.
      % [(1)caster,(2)spellpower,(3)duration,(4)charges,(5)speed,(6)direction,(7)step,(8)noise,(9)twirl]
      lSeeda = [Nth(seed,1),Nth(seed,2),Nth(seed,3),Nth(seed,4),Nth(seed,5),(iDirection+6) MOD 8,1,Nth(seed,8),0];
      lSeedb = [Nth(seed,1),Nth(seed,2),Nth(seed,3),Nth(seed,4),Nth(seed,5),(iDirection+6) MOD 8,1,Nth(seed,8),0];
      lSeedc = [Nth(seed,1),Nth(seed,2),Nth(seed,3),Nth(seed,4),Nth(seed,5),(iDirection+6) MOD 8,1,Nth(seed,8),0];
      lSeedd = [Nth(seed,1),Nth(seed,2),Nth(seed,3),Nth(seed,4),Nth(seed,5),(iDirection+2) MOD 8,1,Nth(seed,8),0];
      lSeede = [Nth(seed,1),Nth(seed,2),Nth(seed,3),Nth(seed,4),Nth(seed,5),(iDirection+2) MOD 8,1,Nth(seed,8),0];
      lSeedf = [Nth(seed,1),Nth(seed,2),Nth(seed,3),Nth(seed,4),Nth(seed,5),(iDirection+2) MOD 8,1,Nth(seed,8),0];

      Send(self,@SpawnElement,#seed=lSeeda,#who=Nth(seed,1),#xoffset=iXStep,#yoffset=iYStep,#walltype=&WallOfFire);
      Send(self,@SpawnElement,#seed=lSeedb,#who=Nth(seed,1),#xoffset=iXStep*2,#yoffset=iYStep*2,#walltype=&WallOfFire);
      Send(self,@SpawnElement,#seed=lSeedc,#who=Nth(seed,1),#xoffset=iXStep*3,#yoffset=iYStep*3,#walltype=&WallOfFire);
      Send(self,@SpawnElement,#seed=lSeedd,#who=Nth(seed,1),#xoffset=iXStep,#yoffset=iYStep,#walltype=&WallOfFire);
      Send(self,@SpawnElement,#seed=lSeede,#who=Nth(seed,1),#xoffset=iXStep*2,#yoffset=iYStep*2,#walltype=&WallOfFire);
      Send(self,@SpawnElement,#seed=lSeedf,#who=Nth(seed,1),#xoffset=iXStep*3,#yoffset=iYStep*3,#walltype=&WallOfFire);

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
