% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Haste is PersonalEnchantment

constants:

   include blakston.khd

resources:

   Haste_name_rsc = "haste"
   Haste_icon_rsc = ihaste.bgf
   Haste_desc_rsc = \
      "The magic of Kraanan grants you nimble feet, reducing the strain of running greatly."
      "Requires edible mushrooms to cast."
   
   Haste_on_rsc = "Your blood begins to tingle and you feel as nimble as a cat."
   Haste_off_rsc = "Your feel the tide of energy in your blood recede, and you seem to slow down."
   Haste_already_enchanted_rsc = "You are already hasted."
   Haste_success_rsc = "%s%s is charged with your magical energy."

   Haste_spell_intro = "Kraanan Lv. 2: Boosts your agility, making it easier to run and avoid attacks."

   Haste_sound = khaste.wav

classvars:

   vrName = Haste_name_rsc
   vrIcon = Haste_icon_rsc
   vrDesc = Haste_desc_rsc

   vrAlreadyEnchanted = Haste_already_enchanted_rsc
   vrEnchantment_On = Haste_On_rsc
   vrEnchantment_Off = Haste_Off_rsc
   vrSuccess = Haste_Success_rsc

   viSpell_num = SID_HASTE
   viSchool = SS_KRAANAN
   viSpell_level = 2
   viMana = 10
   viSpellExertion = 0

   viCast_time = 2000
   viChance_To_Increase = 15

   vrSpell_intro = Haste_spell_intro
   vrSucceed_wav = Haste_sound

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Snack,1],plReagents);

      return;
   }
   
   CanPayCosts(who = $, lTargets = $, bItemCast = FALSE)
   {
      local otarget, i;

      if lTargets = $
      {
         lTargets = [who];
      }
   
      if Length(lTargets) <> 1
      {
         return FALSE;
      }

      otarget = First(lTargets);
      
      % check for eagle eyes already applied
      if Send(otarget,@IsEnchanted,#what=Send(SYS,@FindSpellByNum,#num=SID_FREE_ACTION))
      {
         % Casting on self.
         if who = otarget
         {   
            Send(who,@MsgSendUser,#message_rsc=vrAlreadyEnchanted);
         }
         else
         {
            % Casting on someone else.
            Send(who,@MsgSendUser,#message_rsc=vrOtherAlreadyEnchanted,
            #parm1=send(oTarget,@GetCapDef),#parm2=send(oTarget,@GetName));
            Send(oTarget,@MsgSendUser,#Message_rsc=vrTargetAlreadyEnchanted,
            #parm1=send(who,@GetCapDef),#parm2=send(who,@GetName),
            #Parm3=send(self,@GetCapDef),#Parm4=send(self,@GetName));
         }
        
         return FALSE;
      }
      propagate;
   }

   GetStateValue(iSpellPower=0)
   {
      return iSpellPower;
   }

   SetSpellPlayerFlag(who=$)
   {
      Send(who,@SetPlayerFlag,#flag=PFLAG2_HASTED,#flagset=2,#value=True);
      return;
   }
   
   GetDuration(iSpellpower = 0)
   {
      local iDuration;
      
      iDuration = 1500 + 25 * iSpellPower;
      iDuration = iDuration * 1000;
      
      return random(iDuration/2,iDuration);
   }

   GetPotionClass()
   {
      RETURN &HastePotion;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%