% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%S%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ArmorOfGort is PersonalEnchantment

constants:

   include blakston.khd

resources:

   ArmorOfGort_name_rsc = "armor of Gort"
   ArmorOfGort_icon_rsc = igort.bgf
   ArmorOfGort_desc_rsc = \
      "Surrounds the caster with a powerful field which deflects "
      "damage from extreme blows.  "
      "Requires two blue dragon scales to cast."
   
   ArmorOfGort_already_enchanted_rsc = "You already have the Armor of Gort."

   ArmorOfGort_on_rsc = \
      "Your skin feels taut as the Armor of Gort forms around you."
   ArmorOfGort_off_rsc = "Your Armor of Gort dissipates."
   ArmorOfGort_success_rsc = \
      "%s%s's skin tightens as the Armor of Gort forms."

   ArmorOfGort_spell_intro = \
      "Kraanan Level 5: Magical armor that protects the caster."

classvars:

   vrName = ArmorOfGort_name_rsc
   vrIcon = ArmorOfGort_icon_rsc
   vrDesc = ArmorOfGort_desc_rsc

   vrAlreadyEnchanted = ArmorOfGort_already_enchanted_rsc
   vrEnchantment_On = ArmorOfGort_On_rsc
   vrEnchantment_Off = ArmorOfGort_Off_rsc
   vrSuccess = ArmorOfGort_Success_rsc

   viSpell_num = SID_ARMOR_OF_GORT

   vrSpell_intro = ArmorOfGort_spell_intro

   viMana = 10

   viSpell_level = 5
   viSchool = SS_KRAANAN

   viChance_To_Increase = 20
   viMeditate_ratio = 75
   viFlash = FLASH_GOOD_SELF

   viDefensive = TRUE

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&BlueDragonScale,2],plReagents);

      return;
   }

   GetDuration(iSpellPower=0)
   {
      local iDuration;

      iDuration = iSpellPower/8 + 1;
      % Convert to milliseconds
      iDuration = iDuration * 60 * 1000;

      return iDuration;
   }

   GetStateValue(who=$,iSpellPower=0,Target=$)
   {
      Send(Target,@AddPhysicalReductionFlat,#amount=(iSpellPower+1)*2);

      return iSpellPower;
   }

   PriorityModifyDefenseDamage(damage=$,atype=0,aspell=0)
   {
      local iMax;

      iMax = 1500;

      if aspell <> 0
      {
         if atype <> 0
         {
            iMax = 2000;
         }
         else
         {
            iMax = 2500;
         }
      }

      return bound(damage-iMax,0,$);
   }

   ModifyDefenseDamage(who=$,what=$,damage=$,atype=0,aspell=0)
   {
      return damage;
   }

   EndEnchantment(who=$,report=TRUE,state=0)
   {
      Send(who,@AddPhysicalReductionFlat,#amount=-(state+1)*2);

      propagate;
   }

   EffectDesc(who=$)
   {
      AppendTempString("\n\n");
      AppendTempString("Your current ");
      AppendTempString(Send(self,@GetName));
      AppendTempString(" enchantment adds ");
      AppendTempString((Send(who,@GetEnchantedState,#what=self)+1)/50);
      AppendTempString(" armor or reduces non-magical damage to 15, partially magical damage to 20, and fully magical damage to 25. ");
      AppendTempString(" Only the better of these two effects applies. Armor of Gort modifies damage received before any other armor or spells.");

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
