% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
HolyTouch is TouchAttackSpell

constants:

   include blakston.khd

resources:

   HolyTouch_name_rsc = "holy touch"
   HolyTouch_icon_rsc = iholyt.bgf
   HolyTouch_desc_rsc = \
      "Pure energy from Shal'ille sears your target.  More effective on evil "
      "and undead targets.   "
      "Requires emeralds."

   HolyTouch_already_enchanted_rsc = \
      "Your hands are already bathed in Shal'ille's love."
   HolyTouch_On_rsc = "Your hands are bathed in Shal'ille's love."
   HolyTouch_Off_rsc = "Your hands are no longer bathed in Shal'ille's love."

   HolyTouch_player_was_hit = "%s%s scalds you with the pure force of Shal'ille!"
   HolyTouch_player_hit_something = \
      "%s%s%s shrinks away from the pure energy of Shal'ille."

   HolyTouch_player_killed_something = \
      "%s%s finally succumbs, finding peace through Shal'ille's purifying energy."
   HolyTouch_player_was_killed = \
      "The touch of %s%s separates body from soul and sends you to the underworld."

   holytouch_spell_intro = \
      "Shal'ille Lv. 2: Delivers the judgment of Shal'ille in your touch."

   holytouch_sound = sholytch.wav

   ta_heal_worked = "Shal'ille aids you in battle, healing you for ~g~B%i~B~n damage."
	
classvars:

   vrName = HolyTouch_name_rsc
   vrIcon = HolyTouch_icon_rsc
   vrDesc = HolyTouch_desc_rsc

   vrAlreadyEnchanted = HolyTouch_already_enchanted_rsc
   vrEnchantment_On = HolyTouch_On_rsc
   vrEnchantment_Off = HolyTouch_Off_rsc

   vrSpell_intro = holytouch_spell_intro

   vrPlayer_was_hit = HolyTouch_player_was_hit
   vrPlayer_hit_something = holytouch_player_hit_something
   vrPlayer_killed_something = HolyTouch_player_killed_something
   vrPlayer_was_killed = HolyTouch_player_was_killed

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSchool = SS_SHALILLE
   viSpell_num = SID_HOLY_TOUCH
   viSpellExertion = 3
   viSpell_level = 2
   viMana = 12

   viRange = 2

   viSpellType = ATCK_SPELL_HOLY+ATCK_SPELL_ALL

   vbAutomatic = FALSE

   vrSucceed_wav = holytouch_sound

properties:

   plPrerequisites = $
   plReagents = $

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Emerald,2],plReagents);

      return;
   }

	GetProf(who=$)
	{
		local iProf;

		iProf = bound(Send(who,@GetKarma)/2,0,50) + send(who,@GetMysticism);

		iProf = iProf * bound(Send(who,@GetBaseMaxHealth),0,100) / 100;

		return iProf;
	}
  
   DamageFactors(damage=0,who=$,victim=$)
    "Here, we handle damage adjustments."
   {
		local iKarma, iProf, oWeapon;
		
		%Holy touch won't damage good creatures but deal extra damage to the undead.
		if victim <> $
		{
			iKarma = Send(victim,@GetKarma,#detect=TRUE);

			if iKarma > 0
			{
				damage = 0;
			}
			else
			{
				damage = damage * bound(-(iKarma*2),0,100)/100;
			}
					 
			if Send(victim,@IsUndead)
			{
				damage = damage + damage/2;
			}
		}
		
      oWeapon = Send(who,@LookupPlayerWeapon);
		
		if oWeapon <> $
		{
			iProf = send(oWeapon,@GetProf,#who=who);
		}
		else
		{
			iProf = send(self,@GetProf,#who=who);
		}
		
      % scale damage with the proficiency. See the individual touchattack spell for more info.
		damage = damage + damage * iProf / 100;
		
		% allow tuning through the class variable
		damage = damage*viDamage_factor/100;

		% check for possible procs
		Send(self,@DoSpellProc,#damage=damage,#who=who,#victim=victim,#prof=iProf);
		
      return damage;
   }
  
	DoSpellProc(damage=0,who=$,victim=$,prof=0)
	{
      local iHeal;
		
		if random(1,1000) <= prof*viSpell_proc_chance/100
		{
		 iHeal = random(damage/200,damage/100);
			send(who,@GainHealth,#amount=iHeal);
			send(who,@MsgSendUser,#message_rsc=ta_heal_worked,#parm1=iHeal);
		}

		return;
	}

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
