% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
AcidTouch is TouchAttackSpell

constants:

   include blakston.khd

   POISON_DURATION = 30000 %%% in milliseconds
   POISON_LOSSRATE = 10000 %%% in health points * 10^-4 / second
	
resources:

   AcidTouch_name_rsc = "acid touch"
   AcidTouch_icon_rsc = iacidtou.bgf
   AcidTouch_desc_rsc = \
      "Burns the target with the force of a powerful acid, "
      "penetrating armor, clothing, and flesh.  "
      "Requires entroot berries."

   AcidTouch_On_rsc = "The corrosive spittle of Qor's love oozes from the pores of your hand."
   AcidTouch_Off_rsc = "Your hands no longer drip with acidic ooze."
   AcidTouch_Already_Enchanted_rsc = "Your hands are already dripping with acidic ooze."

   AcidTouch_player_was_hit = "%s%s burns through your flesh with an acid touch."
   AcidTouch_player_hit_something = "%sYou watch %s%s scream in agony at your touch."

   AcidTouch_player_killed_something = "%s%s screams and melts into an unobtrusive puddle."
   AcidTouch_player_was_killed = "At the touch of %s%s, you let out a final scream and die."

   acidtouch_spell_intro = "Qor Lv. 2: Covers caster's hands a powerful acid which penetrates armor, clothing, and flesh."

   acidtouch_sound = qacid.wav
	
   ta_poison_worked_user = "Your acidic touch eats into %s%s's flesh, polluting %s blood!"
   ta_poison_worked_target = "%s%s's acidic touch eats into your flesh and pollutes your blood!"
  
classvars:

   vrName = AcidTouch_name_rsc
   vrIcon = AcidTouch_icon_rsc
   vrDesc = AcidTouch_desc_rsc

   vrAlreadyEnchanted = AcidTouch_already_enchanted_rsc
   vrEnchantment_On = AcidTouch_On_rsc
   vrEnchantment_Off = AcidTouch_Off_rsc

   vrPlayer_was_hit = AcidTouch_player_was_hit
   vrPlayer_hit_something = AcidTouch_player_hit_something

   vrPlayer_was_killed = AcidTouch_player_was_killed
   vrPlayer_killed_something = AcidTouch_player_killed_something

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE
   vrSpell_intro = acidtouch_spell_intro

   viSchool = SS_QOR
   viSpell_num = SID_ACID_TOUCH
   viSpellExertion = 3
   viSpell_level = 2
   viMana = 10

   viSpellType = ATCK_SPELL_ACID + ATCK_SPELL_ALL

   vbAutomatic = FALSE

   vrSucceed_wav = acidtouch_sound

properties:

   plPrerequisites = $
   plReagents = $

messages:

  ResetReagents()
  {
     plReagents = $;
     plReagents = cons([&EntrootBerry,1],plReagents);
     return;
  }

	GetProf(who=$)
	{
		local iProf;

		iProf = bound(-Send(who,@GetKarma)/2,0,50) + send(who,@GetMysticism);
		
		iProf = iProf * bound(Send(who,@GetBaseMaxHealth),0,100) / 100;

		return iProf;
	}
  
	DoSpellProc(damage=0,who=$,victim=$,prof=0)
	{
		local oSpell;
		
		% Poison the victim.
		oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);

		if random(1,1000) <= prof*viSpell_proc_chance/100
		{

			send(oSpell,@MakePoisoned,#who=victim,
				 #lossrate=POISON_LOSSRATE,#duration=POISON_DURATION);
			send(who,@MsgSendUser,#message_rsc=ta_poison_worked_user,
				  #parm1=send(victim,@GetDef),#parm2=send(victim,@GetName),
				  #parm3=send(victim,@GetHisher));
			send(victim,@MsgSendUser,#message_rsc=ta_poison_worked_target,
				  #parm1=send(victim,@GetCapDef),#parm2=send(victim,@GetName));
		}

		return;
	}

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
