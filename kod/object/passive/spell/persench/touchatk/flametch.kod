% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
TouchOfFlame is TouchAttackSpell

constants:

   include blakston.khd

resources:

   TouchOfFlame_name_rsc = "touch of flame"
   TouchOfFlame_icon_rsc = itoucfla.bgf
   TouchOfFlame_desc_rsc = \
      "Boils the innards of the target with a white hot flame.  "
      "Requires red mushrooms to cast."

   TouchOfFlame_already_enchanted_rsc = \
      "Your hands are already shrouded in flame."
   TouchOfFlame_On_rsc = "Jets of flame erupt from your fingers."
   TouchOfFlame_Off_rsc = "Your hands are no longer shrouded in flame."

   TouchOfFlame_player_was_hit = \
      "Flames erupt from %s%s's fingers, burning you!"
   TouchOfFlame_player_hit_something = \
      "%sFlames leap from your fingers and burn %s%s."
   TouchOfFlame_player_killed_something = \
      "%s%s, engulfed in flames, lets out a final scream."
   TouchOfFlame_player_was_killed = \
      "A burst of flames from %s%s's hand is the last thing you see."

   TouchOfFlame_spell_intro = \
      "Faren Lv. 2: Turns your touch into a blaze of searing flame, damaging "
      "your foes."

   TouchOfFlame_sound = fflame.wav
	
   ta_blind_worked = "%s%s stumbles away, screaming and clutching %s eyes!"

classvars:

   vrName = TouchOfFlame_name_rsc
   vrIcon = TouchOfFlame_icon_rsc
   vrDesc = TouchOfFlame_desc_rsc

   vrAlreadyEnchanted = TouchOfFlame_already_enchanted_rsc
   vrEnchantment_On = TouchOfFlame_On_rsc
   vrEnchantment_Off = TouchOfFlame_Off_rsc

   vrPlayer_was_hit = TouchOfFlame_player_was_hit
   vrPlayer_hit_something = TouchOfFlame_player_hit_something

   vrPlayer_killed_something = TouchOfFlame_player_killed_something
   vrPlayer_was_killed = TouchOfFlame_player_was_killed
   
   vrSpell_intro = TouchOfFlame_spell_intro

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSchool = SS_FAREN
   viSpell_num = SID_TOUCH_OF_FLAME
   viSpell_level = 2
   viSpellExertion = 2
   viMana = 9

   viRange = 3

   viSpellType = ATCK_SPELL_FIRE + ATCK_SPELL_ALL

   vbAutomatic = FALSE

   vrSucceed_wav = TouchOfFlame_sound

properties:

   plPrerequisites = $
   plReagents = $

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = cons([&RedMushroom,1],plReagents);
     
      return;
   }

  GetProf(who=$)
  {
     return Send(who,@GetAbility,#num=SID_MANA_FOCUS) * bound(Send(who,@GetBaseMaxHealth),0,100) / 100;
  }
  
	DoSpellProc(damage=0,who=$,victim=$,prof=0)
	{
		local oSpell;
		
		% Blind the victim.
		oSpell = send(SYS,@FindSpellByNum,#num=SID_BLIND);
		
		if random(1,1000) <= prof*viSpell_proc_chance/100
		{
			%% no duplicates
			if not Send(victim,@IsEnchanted,#what=oSpell)
			{        
				send(oSpell,@CastSpell,#who=self,#ltargets=[victim],#iSpellPower=50);
				send(who,@MsgSendUser,#message_rsc=ta_blind_worked,
						#parm1=send(victim,@GetCapDef),#parm2=send(victim,@GetName),
						#parm3=send(victim,@GetHisher));
			}
		}

		return;
	}

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
