% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
denial is PersonalEnchantment

constants:
   include blakston.khd

resources:

   denial_name_rsc = "denial"
   denial_icon_rsc = idenial.bgf
   denial_desc_rsc = \
      "Causes the caster to unrealize their wounds, but beware when "
      "you realize its a hoax, because the wounds are still just as deep!  "
      "Requires solagh to cast."

   denial_on_rsc = "Your wounds suspiciously disappear."
   denial_failed_rsc = "Hmmm, you can't seem to convince yourself of perfect health."
   denial_already_enchanted_rsc = "You can't fool yourself twice about something like this, at "
      "least not at the same time."

   denial_off_rsc = "Ouch - you are reminded of your previous weariness."
	
	denial_warning = "Your realize that your wounds are too deep to keep going.  Your life begins to fade..."
	
   denial_sound = rdenial.wav

classvars:

   vrName = denial_name_rsc
   vrIcon = denial_icon_rsc
   vrDesc = denial_desc_rsc

   vrAlreadyEnchanted = Denial_already_enchanted_rsc
   vrEnchantment_On = Denial_On_rsc
   vrEnchantment_Off = Denial_Off_rsc

   viSpell_num = SID_DENIAL
   viSchool = SS_RIIJA
   viSpell_level = 4
   viMana = 15

   viSpellExertion = 5
   viCast_time = 0

   vbCanCastOnOthers = FALSE

   vrSucceed_wav = denial_sound
	
	% It is viPurge_factor times as hard to purge the spell.
	viPurge_factor = 4

properties:

plDelusionalPeople = $

messages:

   CanPayCosts(who = $, lTargets = $, bItemCast = FALSE)
   {
      % if they are already boosted above max health, then return FALSE
      if send(who,@GetHealth) >= send(who,@GetMaxHealth) 
      {
         if NOT bItemCast
         {
	         send(who,@MsgSendUser,#message_rsc=denial_failed_rsc);
         }
	      
	      return FALSE;
      }

      propagate;
   }
   
   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Solagh,1],plReagents);

      return;
   }
	
   CastSpell(who = $,iSpellPower=0,lTargets=$)
   {
      local i, iHPGain;
      
      iHPGain = send(who,@GetMaxHealth) - send(who,@GetHealth);
		
		for i in plDelusionalPeople
		{
			if Nth(i,1) = who
			{
				plDelusionalPeople = DelListElem(plDelusionalPeople,i);						
			}
		}
		
		plDelusionalPeople = Cons([who,iHPGain],plDelusionalPeople);
		
		propagate;
	}

   GetDuration(iSpellPower = 0)
   {
      return 60000;      
   }


   GetStateValue(who = $, Target = $, iSpellPower = 1)
   {
		local i, iHPGain;
		
		iHPGain = 0;
		
		for i in plDelusionalPeople
		{
			if Nth(i,1) = who
			{
				iHPGain = Nth(i,2);
				break;
			}
		}
		
		iHPGain = iHPGain * iSpellPower / 100;
      
      send(who,@GainHealth,#amount=iHPGain);

      return iSpellPower;
   }


   EndEnchantment(who = $, state=$, report = TRUE)
   {
		local i, iHPGain;
		
		iHPGain = 0;
		
		for i in plDelusionalPeople
		{
			if Nth(i,1) = who
			{
				iHPGain = Nth(i,2);
				
				if report
				{
					plDelusionalPeople = DelListElem(plDelusionalPeople,i);						
				}
				
				break;
			}
		}
		
		iHPGain = iHPGain * state / 100;
	
      post(self,@EndEnchantmentEffects,#who=who,#state=iHPGain,#report=report);
      
      propagate;
   }

   EndEnchantmentEffects(who=$, state=1, report=TRUE)
   {
		if report AND state >= send(who,@GetHealth)
		{
			Send(who,@MsgSendUser,#message_rsc=denial_warning);
         Send(who,@SetPlayerFlag,#flag=PFLAG_NO_MOVE,#value=TRUE);
         Send(who,@SetPlayerFlag,#flag=PFLAG_NO_FIGHT,#value=TRUE);
         Send(who,@SetPlayerFlag,#flag=PFLAG_NO_MAGIC,#value=TRUE);
			Send(who,@EffectSendUser,#effect=EFFECT_PARALYZE_ON);	
			send(who,@StartDenialDeathTimer);
		}
		
		send(who,@LoseHealth,#amount=state);
		
      return;
   } 

   GetPotionClass()
   {
      return &DenialPotion;
   }
	
   IsIllusion()
   {           
		% No simple dispelling here. Makes the spell useless.
      return FALSE;
   }
	
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
