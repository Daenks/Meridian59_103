% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ItemAttTranscendant is ItemAttribute

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%   This item will remain in your inventory, even if you die.
%
%   Form is:
%
%      [IA_TRANSCENDANT]
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

constants:

   include blakston.khd

resources:

   include iatransc.lkod

   itematttranscendant_desc = "  It glows with a natural soft white light."
   itematttranscendant_name = "transcendant %s"

   transcendant_DM = "transcendant"

   transcendant_burned = \
      "A searing flare erupts from the %s held by %s as the item burns away "
      "holy energies to remain with its owner!"

classvars:

   viItem_Att_Num = IA_TRANSCENDANT
   vrDesc = itematttranscendant_desc
   vrName = itematttranscendant_name

   viDifficulty = 3
   vrDM_Trigger = transcendant_DM

properties:

   piValue_modifier = 150
   piValue_power_modifier = 0

messages:

   ItemDropOnDeath()
   {
      return FALSE;
   }

   IsMagicalEffect()
   {
      return TRUE;
   }
   
   OwnerPenned(oItem=$,who=$,oRoom=$)
   {
      Send(self,@ActivateSpecial,#oItem=oItem,#who=who,#oRoom=oRoom);
      return;
   }
   
   OwnerKilled(oItem=$,who=$,oRoom=$)
   {
      Send(self,@ActivateSpecial,#oItem=oItem,#who=who,#oRoom=oRoom);
      return;
   }
   
   ActivateSpecial(oItem=$,who=$,oRoom=$)
   {
      local oObject, oPlayer;

      % Our owner is in the process of dying.
      % Send room a message and remove self.
      % This is a naturally occuring bond, so it does not eat a health point.

      if who <> $
         AND IsClass(who,&Player)
      {
         if oRoom <> $
         {
            foreach oObject in Send(oRoom,@GetHolderActive)
            {
               oPlayer = Send(oRoom,@HolderExtractObject,#data=oObject);
               if IsClass(oPlayer,&Player)
               {
                  Send(oPlayer,@MsgSendUser,#message_rsc=transcendant_burned,
                        #parm1=Send(oItem,@GetName),
                        #parm2=Send(who,@GetName));
               }
            }
         }
      }

      % DropOnDeath has not been checked yet in player. Post this.
      Post(self,@RemoveFromItem,#oItem=oItem);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%