% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

StatFood is Food

constants:

   include blakston.khd

resources:

   statfood_name_rsc = "stat food"
   statfood_name_plural_rsc = "stat food"
   statfood_icon_rsc = mushroom.bgf
   statfood_desc_rsc = "This is some stat food!"

classvars:

   vrName = statfood_name_rsc
   vrName_plural = statfood_name_plural_rsc
   vrIcon = statfood_icon_rsc
   vrDesc = statfood_desc_rsc

   viIndefinite = ARTICLE_AN
   
   viSpellEffect = SID_BLESS

   viBulk = 5
   viWeight = 5
   viValue_average = 50

properties:

   viFilling = 20                
   viNutrition = 10              
   piNumber = 1

messages:

   CastSpell(what = $, apply_on = $)
   "Casts the spell set by the spell object"
   {
      local oSpell, iSpellPower, lTargets;

      oSpell = Send(SYS,@FindSpellByNum,#num=viSpellEffect);

      % Get proper targets according to spell, assume that apply_on is our basic target.
      lTargets = Send(oSpell,@GetTargets,#who=what,#lTargets=[apply_on]);

      iSpellPower = 50;
      
      Send(oSpell,@CastSpell,#who=poOwner,#lTargets=lTargets,
              #iSpellPower=iSpellPower,#bItemCast=TRUE);

      return;
   }

   SendTaste(what = $,apply_on = $)
   {
      Send(apply_on,@MsgSendUser,#message_rsc=food_use);
         return;
   }

   NewApplied(what = $,apply_on = $)
   {
      Send(self,@SendTaste,#what=what,#apply_on=apply_on);
      Send(what,@WaveSendUser,#wave_rsc=vrEat_wav);
      Send(apply_on,@EatSomething,#nutrition=viNutrition,#filling=viFilling);
      Send(self,@SubtractNumber,#number=1);
      Send(self,@CastSpell,#what=what,#apply_on=apply_on);
      return;
   }

   IsAlcohol()
   {        
      return FALSE;
   }

   IsBeverage()
   {
      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
