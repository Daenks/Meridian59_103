% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BetaPotion is PassiveItem

constants:

   include blakston.khd

resources:

   BetaPotion_icon_rsc = potion01.bgf

   BetaPotion_name_rsc = "The infamous BETA POTION!"
   BetaPotion_desc_rsc = "This potion is to facilitate beta testing by allowing "
       "the testers to test the more advanced parts of the game.\n\n"

   BetaPotion_Append_rsc = "This potion will give you a boost of " 
   
   BetaPotion_Append2_rsc = " hit points (but will not raise your health above "
   BetaPotion_Append3_rsc = ").  "
       "The size of the boost is directly related to how much time your "
       "character has spent online.  Thus, beta testers who have devoted "
       "more to the game have been rewarded greater.  "
       "Once you drink the potion, you can also select your schools of magic"
       "by saying the name of the schools and the levels you wish to receive. "
       "Limited by your Intellect you will only receive a certain amount of "
       "points before the potion effect wears off.\n\n"
       "If you lose the potion in any way, shape or form (including if you give "
       "it to a friend and s/he quaffs it, or you step through the portal in "
       "Raza, or your little sister suicides your character just to spite you), "
       "or you just threw it away, you will NOT get another potion.  So don't ask.\n\n"
         
   BetaPotion_gulp_sound = drkptn.wav

   BetaPotion_drink = "You quaff the contents of the vial in a single gulp."
   BetaPotion_worked = "You suddenly feel a little taller."
   BetaPotion_bad_drink = \
      "You choke down the foul-tasting contents of the vial, realizing almost "
      "immediately that this was a pretty bad idea. Time has caused it to sour."
      
   BetaPotion_select_schools = \
      "Add your schools until you are out of points. Say one of the following:\n"
      "~B\"[SHAL'ILLE, QOR, KRAANAN, FAREN, RIIJA, JALA, WEAPONCRAFT]\"~n "
      "followed by ~B\"LEVEL [ONE, TWO, THREE, FOUR, FIVE, SIX]\""

classvars:
   
   vrDesc = BetaPotion_desc_rsc

   viBulk = 20
   viWeight = 20
   viValue_average = 60

   viItem_type = ITEMTYPE_POTION | ITEMTYPE_SUNDRY
   viUse_type = ITEM_SINGLE_USE

   viPoisonPercent = 0
   viGoBadDamage = 0

properties:

   vrName = BetaPotion_name_rsc
   vrIcon = BetaPotion_icon_rsc
   
   piGoBadTime = 0
   piItem_flags = PT_GRAY_TO_RED

   piHealth = 0
   piHealthCap = 100
   pbGiveHealth = FALSE

messages:

   Constructor(health = 0, giveHealth = FALSE, healthCap = 100)
   {
      piHealth = health;
      piHealthCap = healthCap;
      pbGiveHealth = giveHealth;

      propagate;
   }

   AppendDesc()
   {
      local oSpellOrSkill;

      AppendTempString(BetaPotion_Append_rsc);

      if pbGiveHealth
      {
         AppendTempString(piHealth);
      }
      else
      {
         AppendTempString(0);
      }
      
      AppendTempString(BetaPotion_Append2_rsc);
      AppendTempString(piHealthCap);
      AppendTempString(BetaPotion_Append3_rsc);
      
      return;
   }

   ApplyPotionEffects(apply_on = $)
   {
      local HealthBoost, iBaseMaxHealth;

      if Send(Send(SYS,@GetLore),@BetaPotionsEnabled)
         AND Send(Send(SYS,@GetParliament),@BetaPotionsEnabled) 
         OR Send(SYS, @GetChaosNight)
      {
         iBaseMaxHealth = Send(apply_on,@GetBaseMaxHealth);
         Send(apply_on,@MsgSendUser,#message_rsc=BetaPotion_worked);
         if pbGiveHealth AND (iBaseMaxHealth < piHealthCap)
         {
            % How many hps?  Don't give them over potion max, and don't give
            %  them over stamina max.
            HealthBoost = bound(piHealth,0,(100+Send(apply_on,@GetStamina)));
            HealthBoost = bound(HealthBoost,0,(piHealthCap-iBaseMaxHealth));
            Send(apply_on,@GainBaseMaxHealth,#amount=HealthBoost);

            % Don't use iBaseMaxHealth, because it changed.
            if (Send(apply_on,@GetBaseMaxHealth) >= Send(Send(SYS, @GetSettings), @GetPKillEnableHP))
               AND (NOT Send(apply_on,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE))
            {
               Send(apply_on,@PkillEnable);
            }
         }
         
      Send(apply_on,@MsgSendUser,#message_rsc=BetaPotion_select_schools);
      Post(apply_on,@SetPlayerFlag,#flag=PFLAG2_BETA_POTION,#value=TRUE,#flagset=2);

      }
      
      return;
   }

   %%% Old Potion stuff

   ReqNewApply(what = $,apply_on = $)
   {
      if what = apply_on
      {
         return TRUE;
      }
      
      return FALSE;
   }
   
   NewApplied(what = $,apply_on = $)
   {
      local oSpell;
      if isClass(apply_on,&User) 
      {
         Send(apply_on,@waveSenduser,#wave_rsc=BetaPotion_gulp_sound);
      }

      Send(apply_on,@MsgSendUser,#message_rsc=BetaPotion_drink);
      Send(self,@ApplyPotionEffects,#apply_on=apply_on);

      Send(self,@Delete);

      return;
   }

   IsBeverage()
   {
      return TRUE;
   }

   CanIdentify()
   {
      return TRUE;
   }

   RevealHiddenAttributes()
   {
      return FALSE;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
