% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GemSpellpower is Gem

constants:

   include blakston.khd

resources:

   gem_spellpower_name_rsc = "magma crystal"
   gem_spellpower_icon_rsc = ruby.bgf
   gem_spellpower_desc_rsc = \
	"This shard of gemstone mysteriously retains some of the living heat"
    " from its earthen birth."
	
classvars:
   
   vrName = gem_spellpower_name_rsc
   vrIcon = gem_spellpower_icon_rsc
   vrDesc = gem_spellpower_desc_rsc

   viValue_average = 15000
   viWeight = 0
   viBulk = 0

   viSchool = SS_FAREN

properties:

   piPowerStrength = 1
   
messages:

   Constructor()
   {
      local iRandomRoll;
      
      iRandomRoll = Random(1,1000);
      
      if iRandomRoll < 333
      {
         piPowerStrength = 1;
         propagate;
      }
      
      if iRandomRoll < 666
      {
         piPowerStrength = 2;
         propagate;
      }
      
      if iRandomRoll < 950
      {
         piPowerStrength = 3;
         propagate;
      }
      
      if iRandomRoll < 1000
      {
         piPowerStrength = 4;
         propagate;
      }
      
      if iRandomRoll = 1000
      {
         piPowerStrength = 5;
         propagate;
      }
      
      propagate;
   }
   
   GetSchool()
   {
      return viSchool;
   }
   
   GetStrength()
   {
      return piPowerStrength;
   }
   
   SpellPowerHook(who=$,oSpell=$)
   {
      if (oSpell <> $) AND (Send(oSpell,@GetSchool) = Send(self,@GetSchool))
      {
         return Send(self,@GetStrength);
      }
      
      return 0;
   }
   
   AppendDesc()
   {
      AppendTempString(" When attached to armor, it will offer ");
      AppendTempString(piPowerStrength);
      AppendTempString(" ");
      AppendTempString(Send(Send(SYS,@FindSpellByNum,#num=SID_BLINK),
                          @GetSchoolStr,#iSchool=viSchool));
      AppendTempString(" spellpower. ");
      
      propagate;
   }
   
   AttributeDesc()
   {
      AppendTempString("An attached ");
      AppendTempString(Send(self,@GetName));
      AppendTempString(" adds ");
      AppendTempString(piPowerStrength);
      AppendTempString(" ");
      AppendTempString(Send(Send(SYS,@FindSpellByNum,#num=SID_BLINK),
                          @GetSchoolStr,#iSchool=viSchool));
      AppendTempString(" spellpower. ");
      
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
