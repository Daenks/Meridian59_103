% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Gauntlet is Misc

constants:

   include blakston.khd

resources:

  
   gauntlet_name_rsc = "gauntlets"
   gauntlet_icon_rsc = gauntlet.bgf
   gauntlet_desc_rsc = \
      "These polished steel gauntlets offer protection for "
      "hands and forearms allowing you to strike with "
      "greater confidence and force."

   gauntlet_condition_exc = " shine with the glint of perfection."
   gauntlet_condition_exc_mended = " are in excellent condition, but have been patched before."
   gauntlet_condition_good = " are tarnished and scarred but still usable."
   gauntlet_condition_med = " have been harshly dented in several places."
   gauntlet_condition_poor = " are in danger of falling apart.  Large plates of metal have been torn away during battle."
   gauntlet_condition_broken = " are ruined and offer no benefit."
   gauntlet_broken = "Your gauntlets are a tangled mess of metal."

   standardshirt_rightarm_gauntlet_male = bre.bgf
   standardshirt_rightarm_gauntlet_female =brf.bgf
   standardshirt_leftarm_gauntlet_male = ble.bgf
   standardshirt_leftarm_gauntlet_female = blf.bgf

   barearm_rightarm_gauntlet_male = bri.bgf
   barearm_rightarm_gauntlet_female =brj.bgf
   barearm_leftarm_gauntlet_male = bli.bgf
   barearm_leftarm_gauntlet_female = blj.bgf

classvars:
   
   vrItem_broken = gauntlet_broken

   vrName = gauntlet_name_rsc
   vrIcon = gauntlet_icon_rsc
   vrDesc = gauntlet_desc_rsc
   vrPoss_article = object_article_cap_these_rsc
   
   viUse_type = ITEM_USE_GAUNTLET
   viItem_type = ITEMTYPE_ARMOR
   viUse_amount = 1

   viHits_init_min = 600
   viHits_init_max = 800

   viValue_average = 400
   viBulk = 50
   viWeight = 50

   viGround_group = 1
   viInventory_group = 2
   viBroken_group = 3

   vbShow_condition = TRUE
   vrCondition_exc = gauntlet_condition_exc 
   vrCondition_exc_mended = gauntlet_condition_exc_mended 
   vrCondition_good = gauntlet_condition_good 
   vrCondition_med = gauntlet_condition_med 
   vrCondition_poor = gauntlet_condition_poor 
   vrCondition_broken = gauntlet_condition_broken
   
   viAttack_base = 5
   viDamage_base = 5

properties:

messages:

   NewUsed(what = $)
   {
      local i, oFound, iPriority;
      
      oFound=$;
      for i in send(what,@GetPlayerUsing)
      {
         iPriority = send(i,@ItemChangesArms);
         if iPriority <> 0
         {
            if oFound = $ 
            {
               oFound = i;
            }
            else
            {
               if iPriority > send(oFound,@ItemChangesArms)
               {
                  oFound = i;
               }
            }	    
         }	 
      }
      
      if oFound <> $
      {
         send(oFound,@DoPlayerArt); 
      }
      else
      {
         send(what,@ResetPlayerArms);
      }
    
      propagate;
   }

   NewUnused(what = $)
   {
      local i, oFound, iPriority;
      
      oFound=$;
      for i in send(what,@GetPlayerUsing)
      {
         iPriority = send(i,@ItemChangesArms);
         if iPriority <> 0
         {
            if oFound <> $ 
            {
               if iPriority > send(oFound,@ItemChangesArms)
               {
                  oFound = i;
               }
            }
            else
            {
               oFound = i;
            }
         }	 
      }
      
      if oFound <> $
      {
         send(oFound,@DoPlayerArt); 
      }
      else
      {
         send(what,@ResetPlayerArms);
      }

      propagate;
   }

   DefaultValues()
   {
      piDefense_bonus = viDefense_base;
      piAttack_bonus = viAttack_base;
      piDamage_bonus = viDamage_base;

      propagate;
   }

   OverrideArms()
   {
      return TRUE;
   }
   
   GetOverrideRightArm(who=$)
   {  
      local i, bFound;
      
      bFound = FALSE;
      for i in send(who,@GetPlayerUsing)
      {
         if isClass(i,&TankTop)
         {
            bFound = TRUE;
            
            break;
         }
      }
      
      if bFound
      {
         if send(who,@GetGender) = GENDER_MALE
         {
            return barearm_rightarm_gauntlet_male;
         }
      
         return barearm_rightarm_gauntlet_female;
      }      
      
      if send(who,@GetGender) = GENDER_MALE
      {
         return standardshirt_rightarm_gauntlet_male;
      }
      
      return standardshirt_rightarm_gauntlet_female;
   }

   GetOverrideLeftArm(who=$)
   {
      local i, bFound;

      bFound = FALSE;
      for i in send(who,@GetPlayerUsing)
      {
         if isClass(i,&TankTop)
         {
            bFound = TRUE;

            break;
         }
      }
      
      if bFound
      {
         if send(who,@GetGender) = GENDER_MALE
         {
            return barearm_leftarm_gauntlet_male;
         }
      
         return barearm_leftarm_gauntlet_female;
      }

      if send(who,@GetGender) = GENDER_MALE
      {
         return standardshirt_leftarm_gauntlet_male;
      }
      
      return standardshirt_leftarm_gauntlet_female;
   }

   % No alterations when we hit the target....
   DamageGear()
   {
      return;
   }

   ModifyHitRoll(who = $,what = $,hit_roll = $)
   {
      local oWeapon;

      oWeapon = Send(who,@GetWeapon);
	  
	  if oWeapon <> $
	  {
         if IsClass(oWeapon,&RangedWeapon)
         {
            return 0;
         }
      }
      return piAttack_bonus;
   }
   
   ModifyDamage(who = $,what = $,damage = $)
   {
      local oWeapon;
      
      oWeapon = Send(who,@GetWeapon);
	  
	  if oWeapon <> $
	  {
         if IsClass(oWeapon,&RangedWeapon)
         {
            return 0;
         }
      }

      return piDamage_bonus;
   }

   CanMend()
   { 
      local i, oItemAtt;
		
      for i in plItem_Attributes
      {
         oItemAtt = send(SYS,@FindItemAttByNum,
                         #num=send(self,@GetNumFromCompound,#compound=first(i)));
         if oItemAtt = $
         {
            DEBUG("Illegal ItemAtt in list!");
            
            continue;
         }
	 
         if NOT send(oItemAtt,@ItemCanMend,#oItem=self)
         {
            return FALSE;
         }   
      }
      
      return TRUE; 
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


